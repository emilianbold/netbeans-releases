<?xml version="1.0" encoding="UTF-8"?>
<!--
DO NOT ALTER OR REMOVE COPYRIGHT NOTICES OR THIS HEADER.

Copyright 1997-2010 Oracle and/or its affiliates. All rights reserved.

Oracle and Java are registered trademarks of Oracle and/or its affiliates.
Other names may be trademarks of their respective owners.


The contents of this file are subject to the terms of either the GNU
General Public License Version 2 only ("GPL") or the Common
Development and Distribution License("CDDL") (collectively, the
"License"). You may not use this file except in compliance with the
License. You can obtain a copy of the License at
http://www.netbeans.org/cddl-gplv2.html
or nbbuild/licenses/CDDL-GPL-2-CP. See the License for the
specific language governing permissions and limitations under the
License.  When distributing the software, include this License Header
Notice in each file and include the License file at
nbbuild/licenses/CDDL-GPL-2-CP.  Oracle designates this
particular file as subject to the "Classpath" exception as provided
by Oracle in the GPL Version 2 section of the License file that
accompanied this code. If applicable, add the following below the
License Header, with the fields enclosed by brackets [] replaced by
your own identifying information:
"Portions Copyrighted [year] [name of copyright owner]"

Contributor(s):

The Original Software is NetBeans. The Initial Developer of the Original
Software is Sun Microsystems, Inc. Portions Copyright 1997-2006 Sun
Microsystems, Inc. All Rights Reserved.

If you wish your version of this file to be governed by only the CDDL
or only the GPL Version 2, indicate your decision by adding
"[Contributor] elects to include this software in this distribution
under the [CDDL or GPL Version 2] license." If you do not indicate a
single choice of license, a recipient has the option to distribute
your version of this file under either the CDDL, the GPL Version 2 or
to extend the choice of license to its licensees as provided above.
However, if you add GPL Version 2 code and therefore, elected the GPL
Version 2 license, then the option applies only if the new code is
made subject to such option by the copyright holder.
-->
<xsl:stylesheet xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
     xmlns:wsdl="http://schemas.xmlsoap.org/wsdl/"
     xmlns:soap="http://schemas.xmlsoap.org/wsdl/soap/" 
     xmlns:mime="http://schemas.xmlsoap.org/wsdl/mime/"
     xmlns:http="http://schemas.xmlsoap.org/wsdl/http/" 
     version="1.0">
<xsl:output method="xml" indent="yes"/>
<xsl:strip-space elements="*"/>

<!-- global parameter for web service name passed in from Java -->
<xsl:param name="WSNAME"/>
<!-- global parameter for soap binding passed in from Java-->
<xsl:param name="SOAPBINDING"/>
<!-- global parameter for portType name  passed in from Java-->
<xsl:param name="PORTTYPENAME"/>

  <xsl:template match="wsdl:definitions">
   <xsl:copy>
       <xsl:copy-of select="@*"/>
       <xsl:apply-templates select="wsdl:documentation | wsdl:types | wsdl:import | wsdl:message | wsdl:portType"/>
       <xsl:choose>
         <xsl:when test="string-length($SOAPBINDING)>0">
           <xsl:apply-templates select="wsdl:binding"/>
         </xsl:when>
         <xsl:otherwise>
           <xsl:call-template name="genBinding"/>
         </xsl:otherwise>
       </xsl:choose>
       
       <xsl:call-template name="genService"/> 
   </xsl:copy>
   
  </xsl:template>

   <xsl:template match="wsdl:documentation | wsdl:types | wsdl:import | wsdl:message | wsdl:portType | wsdl:binding">
   <xsl:copy>
       <xsl:copy-of select="@* | node()"/>
       <!-- <xsl:apply-templates/> -->
   </xsl:copy>
  </xsl:template>

  <!-- This is called only if no soap binding is present in the wsdl -->
  <xsl:template name="genBinding">
      <xsl:variable name="targetNS">
         <xsl:value-of select="/wsdl:definitions/@targetNamespace"/> 
      </xsl:variable>
      <xsl:variable name="targetNSPrefix">
        <xsl:value-of select="name(namespace::*[.=$targetNS][1])"/>
      </xsl:variable>
      <xsl:element name="wsdl:binding">
         <xsl:attribute name="name">
           <xsl:value-of select="$PORTTYPENAME"/><xsl:text>Binding</xsl:text> 
         </xsl:attribute>
         <xsl:attribute name="type">
           <xsl:value-of select="$targetNSPrefix"/><xsl:text>:</xsl:text><xsl:value-of select="$PORTTYPENAME"/>   
         </xsl:attribute>
         <xsl:element name="binding" namespace="http://schemas.xmlsoap.org/wsdl/soap/">
            <xsl:attribute name="transport">
               <xsl:text>http://schemas.xmlsoap.org/soap/http</xsl:text>  
            </xsl:attribute>
            <xsl:attribute name="style">
              <xsl:text>document</xsl:text> 
            </xsl:attribute>
         </xsl:element>
         <xsl:for-each select="wsdl:portType">
           <xsl:variable name="sPortTypeName">
             <xsl:value-of select="@name"/>
           </xsl:variable>
           <xsl:if test="$PORTTYPENAME=$sPortTypeName">
         <xsl:for-each select="wsdl:operation"> 
           <xsl:element name="wsdl:operation">  
              <xsl:attribute name="name">
                <xsl:value-of select="@name"/>
              </xsl:attribute>
              <xsl:element name="operation"  namespace="http://schemas.xmlsoap.org/wsdl/soap/" >
                <xsl:attribute name="soapAction">
                  <xsl:text></xsl:text> 
                </xsl:attribute> 
              </xsl:element> 
             <xsl:if test="wsdl:input">
               <xsl:element name="wsdl:input">
                 <xsl:element name="body" namespace="http://schemas.xmlsoap.org/wsdl/soap/">
                   <xsl:attribute name="use">
                     <xsl:text>literal</xsl:text> 
                   </xsl:attribute>
                 </xsl:element>
               </xsl:element>
             </xsl:if>  
             <xsl:if test="wsdl:output">
               <xsl:element name="wsdl:output">
                 <xsl:element name="body" namespace="http://schemas.xmlsoap.org/wsdl/soap/">
                   <xsl:attribute name="use">
                     <xsl:text>literal</xsl:text> 
                   </xsl:attribute>
                 </xsl:element>
               </xsl:element>
             </xsl:if>  
             <xsl:for-each select="wsdl:fault">
                <xsl:element name="wsdl:fault">
                  <xsl:attribute name="name">
                    <xsl:value-of select="@name"/> 
                  </xsl:attribute> 
                  <xsl:element name="fault" namespace="http://schemas.xmlsoap.org/wsdl/soap/">
                    <xsl:attribute name="name">
                      <xsl:value-of select="@name"/>
                    </xsl:attribute>
                    <xsl:attribute name="use">
                      <xsl:text>literal</xsl:text>
                    </xsl:attribute>
                  </xsl:element> 
                </xsl:element>
             </xsl:for-each>
           </xsl:element>
         </xsl:for-each>
         </xsl:if>
        </xsl:for-each>   
      </xsl:element>
  </xsl:template> 

  <xsl:template name="genService">
    <xsl:element name="wsdl:service">
      <xsl:attribute name="name">
         <xsl:value-of select="$WSNAME"/>  
      </xsl:attribute>
        <xsl:element name="wsdl:port">
          <xsl:attribute name="name">
            <xsl:value-of select="$PORTTYPENAME"/><xsl:text>Port</xsl:text>
          </xsl:attribute> 
          <xsl:variable name="targetNS">
            <xsl:value-of select="/wsdl:definitions/@targetNamespace"/> 
          </xsl:variable>
          <xsl:variable name="targetNSPrefix">
            <xsl:value-of select="name(namespace::*[.=$targetNS][1])"/>
          </xsl:variable>
          <xsl:attribute name="binding">
            <xsl:value-of select="$targetNSPrefix"/><xsl:text>:</xsl:text>
            <xsl:choose>
            <xsl:when test="string-length($SOAPBINDING)>0">
              <xsl:value-of select="$SOAPBINDING"/>
            </xsl:when>
            <xsl:otherwise>            
             <xsl:value-of select="$PORTTYPENAME"/><xsl:text>Binding</xsl:text>
            </xsl:otherwise> 
            </xsl:choose>
          </xsl:attribute>
          <xsl:element name="address" namespace="http://schemas.xmlsoap.org/wsdl/soap/">
            <xsl:attribute name="location">
              <xsl:text>PUT ACTUAL ADDRESS HERE</xsl:text>
            </xsl:attribute>   
          </xsl:element>
        </xsl:element> 
    </xsl:element> 
  </xsl:template>
</xsl:stylesheet> 
